plugins {
  id 'java-library'

  // To create a fat jar build/libs/...-all.jar, run: ./gradlew shadowJar
  id 'com.gradleup.shadow' version '8.3.8'

  // Code formatting; defines targets "spotlessApply" and "spotlessCheck"
  // Version 6.14.0 and later requires JRE 11+, but version 6.13.0 doesn't work on JRE 21.
  id 'com.diffplug.spotless' version '6.13.0'

  // Error Prone linter
  id('net.ltgt.errorprone') version '4.3.0'

  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.6.55'

  // To show task list as a tree, run: ./gradlew <taskname> taskTree
  id 'com.dorongold.task-tree' version '4.0.1'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

ext {
  errorproneVersion = '2.39.0'
  isJava17orHigher = JavaVersion.current() >= JavaVersion.VERSION_17
  isJava21orHigher = JavaVersion.current() >= JavaVersion.VERSION_21
}

dependencies {
  api 'org.checkerframework.annotatedlib:bcel:6.5.0'
  // For a locally-built commons-bcel, set $BCEL and use this line instead of the above.
  // implementation fileTree(dir: "$System.env.BCEL/target", include: 'bcel-6.2.0.2.jar')

  implementation 'org.plumelib:reflection-util:1.1.5'

  testImplementation("org.junit.jupiter:junit-jupiter:5.13.2")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

// To upload to Maven Central, see instructions in the file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter'
    excludeEngines 'junit-vintage'
  }
}

if (! isJava21orHigher) {
  spotless {
    format 'misc', {
      // define the files to apply `misc` to
      target '*.md', '.gitignore'

      // define the steps to apply to those files
      trimTrailingWhitespace()
      indentWithSpaces(2) // in Spotless version 7, change to: leadingTabsToSpaces(2)
      endWithNewline()
    }
    java {
      targetExclude('**/WeakIdentityHashMap.java')
      googleJavaFormat()
      formatAnnotations()
    }
    groovyGradle {
      target '**/*.gradle'
      greclipse()  // which formatter Spotless should use to format .gradle files.
      indentWithSpaces(2) // in Spotless version 7, change to: leadingTabsToSpaces(2)
      trimTrailingWhitespace()
      // endWithNewline() // Don't want to end empty files with a newline
    }
  }
}

// Error Prone linter

dependencies {
  errorprone("com.google.errorprone:error_prone_core:${errorproneVersion}")
}
tasks.withType(JavaCompile).configureEach {
  // -processing: suppresses "No processor claimed any of these annotations"
  // -options: suppresses "target value 8 is obsolete and will be removed in a future release"
  options.compilerArgs << '-Xlint:all,-processing,-options' << '-Werror'
  options.errorprone {
    disable('ExtendsObject')  // Incorrect when using the Checker Framework
    disable('ReferenceEquality') // Use Interning Checker instead.
    disable('AnnotateFormatMethod') // Error Prone doesn't know about Checker Framework @FormatMethod
    // Code copied from BCEL that we don't want to change gratuitously.
    excludedPaths = '.*/org/plumelib/bcelutil/StackVer.java'
  }
  options.errorprone.enabled = isJava17orHigher
}

// Checker Framework pluggable type-checking

apply plugin: 'org.checkerframework'

checkerFramework {
  checkers = [
    // No need to run CalledMethodsChecker, because ResourceLeakChecker does so.
    // 'org.checkerframework.checker.calledmethods.CalledMethodsChecker',
    'org.checkerframework.checker.formatter.FormatterChecker',
    // TODO: 'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.interning.InterningChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.regex.RegexChecker',
    'org.checkerframework.checker.resourceleak.ResourceLeakChecker',
    'org.checkerframework.checker.signature.SignatureChecker',
    'org.checkerframework.checker.signedness.SignednessChecker',
    'org.checkerframework.common.initializedfields.InitializedFieldsChecker',
  ]
  extraJavacArgs = [
    '-Werror',
    '-AcheckPurityAnnotations',
    '-ArequirePrefixInWarningSuppressions',
    '-AwarnRedundantAnnotations',
    '-AwarnUnneededSuppressions',
  ]
}
// To use a snapshot version of the Checker Framework.
if (false) {
  // TODO: Change the above test to false when CF is released.
  ext.checkerFrameworkVersion = '3.49.5'
  dependencies {
    compileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    testCompileOnly "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
    checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
  }
  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
  }
}
// To use a locally-built Checker Framework, run gradle with "-PcfLocal".
if (project.hasProperty('cfLocal')) {
  def cfHome = String.valueOf(System.getenv('CHECKERFRAMEWORK'))
  dependencies {
    compileOnly files(cfHome + '/checker/dist/checker-qual.jar')
    testCompileOnly files(cfHome + '/checker/dist/checker-qual.jar')
    checkerFramework files(cfHome + '/checker/dist/checker.jar')
  }
}

// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
  exclude 'org/plumelib/bcelutil/StackVer.java'
  options.with {
    // "linksOffline 'https://docs.oracle.com/javase/8/docs/api/', ..."
    // no longer works, because that URL redirects to https://docs.oracle.com/en/java/javase/19/ but not to any specific webpage under it.
    // "linksOffline 'https://docs.oracle.com/en/java/javase/17/docs/api/', ..."
    // does not works under JDK <= 17.
    // Under JDK 11 and JDK 17, even with "-source 8", it yields error:
    //   The code being documented uses packages in the unnamed module, but the packages defined in
    //   https://docs.oracle.com/en/java/javase/17/docs/api/ are in named modules.
    // See https://bugs.openjdk.org/browse/JDK-8274639
    // JDK 18 has a "--link-modularity-mismatch" command-line option which is "info" or "warn".
    // I think it would work to:
    //  * use JDK 11 links under JDK <= 17.
    //  * use JDK 17 links, and "--link-modularity-mismatch info", under JDK > 17 (i.e., JDK >= 18).
    // But it's easier to just not use "linksOffline".
  }
  options.addStringOption('source', '11')
  doLast {
    ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:'',
    flags:'g', byline:true) {
      fileset(dir: destinationDir)
    }
  }
}
check.dependsOn javadoc

task javadocWeb(type: Javadoc) {
  description = 'Upload API documentation to website.'
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/${project.name}/api")
  classpath = project.sourceSets.main.compileClasspath
  options.addStringOption('source', '11')
  doLast {
    ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:'',
    flags:'g', byline:true) {
      fileset(dir: destinationDir)
    }
    // Set permissions
    project.exec {
      commandLine('chgrp', '-R',  'plse_www', "/cse/web/research/plumelib/${project.name}/api")
    }
    project.exec {
      commandLine('chmod', '-R',  'g+w', "/cse/web/research/plumelib/${project.name}/api")
    }
  }
}

configurations {
  requireJavadoc
}
dependencies {
  requireJavadoc 'org.plumelib:require-javadoc:2.0.0'
}
task requireJavadoc(type: JavaExec) {
  group = 'Documentation'
  description = 'Ensures that Javadoc documentation exists.'
  mainClass = 'org.plumelib.javadoc.RequireJavadoc'
  classpath = configurations.requireJavadoc
  args 'src/main/java'
  jvmArgs += [
    '--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
    '--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
    '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
    '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
  ]
}
check.dependsOn requireJavadoc
javadocWeb.dependsOn requireJavadoc


// Git hooks

task installGitHooks(type: Copy) {
  description = 'Copies git hooks to .git directory'
  from 'githooks'
  into '.git/hooks'
}

tasks.withType(JavaCompile) {
  dependsOn(':installGitHooks')
}

// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description = 'Run etags to create an Emacs TAGS table'
  commandLine 'bash', '-c', "find src/ -name '*.java' | sort | xargs etags"
}
