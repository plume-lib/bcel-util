plugins {
  // To create a fat jar build/libs/bcel-util-all.jar, run: ./gradlew shadowJar
  id "com.github.johnrengelman.shadow" version "2.0.4"
  // Run the Error Prone linter.
  id "net.ltgt.errorprone-base" version "0.0.16"
  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.2.0'
  // Formatting is disabled due to poor handling of type annotations; wait until
  // this issue is fixed: https://github.com/google/google-java-format/issues/5
  // // Code formatting with Google Java Format; defines targets "goJF" and "verJF".
  // id "com.github.sherter.google-java-format" version "0.7.1"
  // To show task list as a tree, run: ./gradlew <taskname> taskTree
  id "com.dorongold.task-tree" version "1.3.1"
}

apply plugin: 'java-library'

// To upload to Maven Central, see instructions in the file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

dependencies {
  implementation 'org.plumelib:reflection-util:0.0.4'
  api group: 'org.checkerframework.annotatedlib', name: 'bcel', version: '6.2.0.2'
  // For a locally-built commons-bcel, set $BCEL and use this line instead of the above.
  // implementation fileTree(dir: "$System.env.BCEL/target", include: 'bcel-6.2.0.2.jar')
  testImplementation 'junit:junit:4.12'
  errorprone 'com.google.errorprone:error_prone_core:2.3.3'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


task compileAll() {
  dependsOn compileJava
  dependsOn compileTestJava
}

// Checker Framework pluggable type-checking
apply plugin: 'org.checkerframework'

checkerFramework {
  checkers = [
    'org.checkerframework.checker.formatter.FormatterChecker',
    'org.checkerframework.checker.interning.InterningChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.nullness.NullnessRawnessChecker',
    'org.checkerframework.checker.regex.RegexChecker',
    'org.checkerframework.checker.signature.SignatureChecker'
  ]
  extraJavacArgs = [
    '-AcheckPurityAnnotations'
  ]
}


/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  // Can't use `-private` because StackVer.java (which is copied from elsewhere) lacks some
  // documentation.
  // options.addStringOption('private', '-quiet')
  options.with {
    linksOffline 'https://docs.oracle.com/javase/8/docs/api/', 'https://docs.oracle.com/javase/8/docs/api/'
    // this works locally but not on Travis?
    //linksOffline 'https://commons.apache.org/proper/commons-bcel/apidocs/', 'https://commons.apache.org/proper/commons-bcel/apidocs/'
  }
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/bcel-util/api")
  classpath = project.sourceSets.main.compileClasspath
}

// `requireJavadoc` target ensures that all Java elements have Javadoc documentation.
dependencies {
  compileOnly group: 'org.plumelib', name: 'require-javadoc', version: '0.1.2'
}
task requireJavadoc(type: Javadoc) {
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
}


/// Git hooks

task installGitHooks(type: Copy) {
  description 'Copies git hooks to .git directory'
  from 'githooks'
  into '.git/hooks'
}

tasks.withType(JavaCompile) {
  dependsOn(':installGitHooks')
}


/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
