// REQUIREMENTS to make a release:
//  * Perform work on a machine connected to the CSE filesystem.
//  * Your ~/.gradle/gradle.properties file must contain:
//     ossrhUsername=YOUR_USER_NAME_HERE
//     ossrhPassword=YOUR_PASSWORD_HERE

// To make a release:
//  * git pull
//  * Update the version number in ../README.md and in this file (multiple times in each).
//  * Update ../CHANGELOG.md .
//  * Run in the top-level directory:  ./gradlew clean javadocWeb && ./gradlew clean uploadArchives
//  * Browse to https://oss.sonatype.org/#stagingRepositories, complete the Maven Central release.
//  * Stage changes.
//  * Add a git tag:
//    VER=1.1.9 && git commit -m "Version $VER" && git push && git tag -a v$VER -m "Version $VER" && git push && git push --tags
//  * Make a GitHub release. Go to the GitHub releases page, make a release, call it "bcel-util 1.1.9", use the changelog text as the description, attach the .jar file from ../build/libs/


///////////////////////////////////////////////////////////////////////////
/// Maven Central publishing
/// (From http://central.sonatype.org/pages/gradle.html )

apply plugin: 'maven'
apply plugin: 'signing'

group = "org.plumelib"
archivesBaseName = "bcel-util"
version = "1.1.9"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { project.gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  doFirst {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Plume-lib Bcel-Util'
          packaging 'jar'
          description 'Utility functions for BCEL.'
          url 'https://github.com/plume-lib/bcel-util'

          scm {
            connection 'scm:git:git@github.com:plume-lib/bcel-util.git'
            developerConnection 'scm:git:git@github.com:plume-lib/bcel-util.git'
            url 'git@github.com:plume-lib/bcel-util.git'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id 'mernst'
              name 'Michael Ernst'
              email 'mernst@alum.mit.edu'
            }
          }
        }
      }
    }
  }
}

// The *-all.jar file isn't used by Maven/Gradle users.  The point of publishing
// it is to host all artifacts on Maven Central, for people who want *-all.jar.
// publishing  {
//   publications {
//     shadow(MavenPublication) {
//       from components.shadow
//       artifactId = 'bcel-util-all'
//   }
// }
